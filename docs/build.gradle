plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

asciidoctor {
    resources {
        from('src/docs/images')
        into "./images"
    }

    attributes 'experimental'  : 'true',
            'compat-mode'   : 'true',
            'toc'           : 'left',
            'icons'         : 'font',
            'version'       : project.version,
            'sourcedir'     : "${rootProject.allprojects.find { it.name == 'http-core'}.projectDir}/src/main/groovy"
}

configurations {
    documentation
}

dependencies {
    documentation project(':http-core')
    documentation project(':http-client')
    documentation "org.codehaus.groovy:groovy-all:$groovyVersion"
}



task apidocs(type: Groovydoc) {
    def allProjects = rootProject.allprojects.findAll { project ->
        ['http-core', 'http-client'].contains(project.name)
    }
    source allProjects.collect { project ->
        project.files('src/main/groovy')
    }

    destinationDir = new File(buildDir, 'docs/api')
    // Might need a classpath
    docTitle = "Grails Views ${version}"

    classpath = configurations.documentation
    groovyClasspath = configurations.documentation
}

task docs(type:Copy, dependsOn:[apidocs, asciidoctor])  {
    from "$buildDir/asciidoc/html5"
    into "$buildDir/docs"
}