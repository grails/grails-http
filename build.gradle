buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}



ext {
    groovyVersion = "2.4.6"
    repo = 'core'
    desc = 'Utility classes for working with the HTTP protocol'
    websiteUrl = 'https://github.com/grails/grails-http'
    issueTrackerUrl = 'https://github.com/grails/grails-http/issues'
    vcsUrl = 'https://github.com/grails/grails-http'
    pomInfo = {
        delegate.name 'Grails HTTP Utils'
        delegate.description desc
        delegate.url 'http://grails.org/'

        delegate.licenses {
            delegate.license {
                delegate.name 'The Apache Software License, Version 2.0'
                delegate.url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                delegate.distribution 'repo'
            }
        }

        delegate.scm {
            delegate.url 'scm:git@github.com:grails/grails-http.git'
            delegate.connection 'scm:git@github.com:grails/grails-http.git'
            delegate.developerConnection 'scm:git@github.com:grails/grails-http.git'
        }


        delegate.developers {
            delegate.developer {
                delegate.id 'graemerocher'
                delegate.name 'Graeme Rocher'
            }
        }

    }
}

subprojects { project ->

    version "1.0.0-SNAPSHOT"


    def parentDir = project.projectDir.parentFile.name
    boolean isPlugin = parentDir.equals('plugins')
    if(isPlugin) {
        group "org.grails.plugins"

        apply plugin:"org.grails.grails-plugin"
        apply plugin:"org.grails.grails-plugin-publish"
        apply plugin:"org.grails.grails-gsp"

        dependencyManagement {
            imports {
                mavenBom "org.grails:grails-bom:$grailsVersion"
            }
            applyMavenExclusions false
        }

        grailsPublish {
            githubSlug = 'grails/grails-http'
            license {
                name = 'Apache-2.0'
            }
            title = "Grails Async HTTP Client"
            desc = "A non-blocking HTTP client for Grails based on Netty"
            developers = [graemerocher:"Graeme Rocher"]
        }

    }
    else {
        group 'org.grails'

        apply plugin:"groovy"
        apply plugin:'maven-publish'
        apply plugin: 'com.jfrog.bintray'
        apply plugin: 'com.bmuschko.nexus'
        apply from:'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/bintrayPublishing.gradle'



        if(project.name != 'http-docs') {

            publishing {
                publications {
                    maven(MavenPublication) {
                        pom.withXml {
                            def pomNode = asNode()

                            pomNode.children().last() + pomInfo
                        }
                        artifactId project.name
                        from components.java
                        artifact sourcesJar
                        artifact javadocJar
                    }
                }

                repositories {
                    maven {
                        credentials {
                            username System.getenv('GRAILS_CENTRAL_USERNAME') ?: project.properties.get('grailsPluginsUsername')
                            password System.getenv("GRAILS_CENTRAL_PASSWORD") ?: project.properties.get('grailsPluginsPassword')
                        }

                        if(version.endsWith('-SNAPSHOT')) {
                            url "https://repo.grails.org/grails/libs-snapshots-local"
                        }
                    }
                }

                dependencies {
                    compile "org.codehaus.groovy:groovy:$groovyVersion"
                }
            }
        }
    }


    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }





}
